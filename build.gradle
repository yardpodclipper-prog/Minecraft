plugins {
    id 'fabric-loom' version '1.8.13'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    mavenCentral()
    maven { url = 'https://api.modrinth.com/maven' }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "maven.modrinth:cobblemon:${project.cobblemon_modrinth_version}"

    include implementation("org.xerial:sqlite-jdbc:3.44.1.0")

    testImplementation platform("org.junit:junit-bom:5.11.4")
    testImplementation "org.junit.jupiter:junit-jupiter"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

tasks.test {
    useJUnitPlatform()
}

def remapJarTask = tasks.named("remapJar", net.fabricmc.loom.task.RemapJarTask)
def releaseJar = remapJarTask.flatMap { it.archiveFile }

tasks.register("testJar", Copy) {
    dependsOn tasks.named("jar")
    from(tasks.named("jar", Jar).flatMap { it.archiveFile })
    into(layout.buildDirectory.dir("libs"))
    rename { "${base.archivesName.get()}-${project.version}-test.jar" }
}

tasks.register("verifyReleaseJar") {
    dependsOn remapJarTask

    doLast {
        File remapped = releaseJar.get().asFile
        if (!remapped.exists()) {
            throw new GradleException("Release jar was not produced by remapJar: ${remapped}")
        }

        if (remapped.length() <= 0L) {
            throw new GradleException("Invalid release artifact at ${remapped}: file is empty.")
        }
    }
}

tasks.named("check") {
    dependsOn tasks.named("verifyReleaseJar")
}

tasks.named("build") {
    dependsOn tasks.named("testJar")
}
